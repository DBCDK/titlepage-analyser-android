plugins {
    id "com.android.application" version "3.3.0"
    id "org.sonarqube" version "2.6"
    id "jacoco"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "dk.dbc.titlepages"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        lintConfig file("lint.xml")
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "titlepage-sender"
        def jacocoReports = []
        fileTree("${buildDir}/outputs/code_coverage").forEach({f ->
            if(f.path.endsWith(".ec") || f.path.endsWith(".exec")){
                jacocoReports << f
            }
        })
        property "sonar.jacoco.reportPaths", jacocoReports
        property "sonar.java.test.binaries", "${buildDir}/intermediates/javac/debug/compileDebugJavaWithJavac/classes"
    }
}

ext {
    android_support_design_version = "28.0.0"
    androidx_test_rules_version = "1.1.1"
    androidx_test_ext_junit_version = "1.1.0"
    constraint_layout_version = "1.1.3"
    espresso_version = "3.1.1"
    junit_version = "4.12"
    lightweight_stream_api_version = "1.2.1"
    uiautomator_version = "2.2.0"
}

dependencies {
    implementation group: "com.android.support.constraint",
        name: "constraint-layout", version: constraint_layout_version
    implementation group: "com.android.support", name: "design",
        version: android_support_design_version
    implementation group: "com.annimon", name: "stream",
        version: "$lightweight_stream_api_version"
    implementation group: "androidx.test.espresso",
        name: "espresso-idling-resource", version: espresso_version
    testImplementation group: "junit", name: "junit", version: junit_version
    androidTestImplementation group: "androidx.test.ext", name: "junit",
        version: androidx_test_ext_junit_version
    androidTestImplementation group: "androidx.test.espresso",
        name: "espresso-core", version: espresso_version
    androidTestImplementation group: "androidx.test.espresso",
        name: "espresso-intents", version: espresso_version
    androidTestImplementation group: "androidx.test",
        name: "rules", version: androidx_test_rules_version
    androidTestImplementation group: "androidx.test.uiautomator",
        name: "uiautomator", version: uiautomator_version
}
